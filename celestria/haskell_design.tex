\section{Design of the sound scene DSL}
\label{sec:design-sound-spec}


From the overall design decisions for the entire sound system and game
system, some parts of the design of the sound scene DSL subsystem were
clear: we had to interact with a \texttt{Redis} database and with
an \texttt{AMQP} communication system, and we needed to send JSON packages according to a
fixed format in order to control the lower level system.

On top of this, we had requirements for swift development, ease of
use, and minimizing the amount of extra parsers to write. These
criteria were central in selecting Haskell as a platform for the tool:
out of the platforms that members of the workgroup were familiar with,
Haskell was far more capable of quick development and quick automatic
generation of parsers and serializers than all the alternatives. 

Our system ended up depending on a family of Haskell packages that
cover many of the interoperability requirements. In total, we relied
on 
\texttt{aeson}~\cite{aeson}, 
\texttt{amqp}~\cite{amqp}, 
\texttt{attoparsec}~\cite{attoparsec}, 
\texttt{base}~\cite{haskell}, 
\texttt{bytestring}~\cite{bytestring},
\texttt{containers}~\cite{containers}, 
\texttt{ghc-prim}~\cite{haskell}, 
\texttt{hedis}~\cite{hedis},
\texttt{mtl}~\cite{mtl}, 
\texttt{regex-posix}~\cite{regex-posix}, 
\texttt{text}~\cite{text}, and
\texttt{unordered-containers}~\cite{unordered-containers}, 
for all our library needs.

From these preconditions, we decided that the best way to construct a
DSL would be to encode all important information in
terms of specific Haskell datatypes, so that Haskell methods for
generating parsers and serializers could be used. Additionally,
we needed a hierarchy of types bridging the gap
between the human-readable game master facing DSL and the machine facing
(already defined) JSON protocol.

The resulting hierarchy of datatypes that we decided on was:\nopagebreak

\begin{figure}[H]
  \centering
  \includegraphics{figure}
  \vspace{1em}
  
  \caption{Dependency and compilation path hierarchy for the sound system.}
\label{fig:sshierarchy}
\end{figure}
where we had separate compilation steps to transform a \texttt{SoundCommand}
into a \texttt{DaemonSpec}, a \texttt{DaemonSpec} into a \texttt{DaemonCommand} and a
\texttt{DaemonCommand} into an \texttt{AMQPDaemon}.

These types all had different roles:
\begin{description}
\item[\tt SoundCommand] encoded all orders the system expected from
  game masters and sound designers. In order to more easily design
  datatypes, we separated out the descriptions of a sound scene and of
  a reaction trigger from the command type into the subordinate types
  \texttt{SoundSpec} and \texttt{FilterSpec}.
\item[\tt DaemonSpec] encoded, abstractly, the order types the lower
  level system accepts.
\item[\tt DaemonCommand] encoded in full detail a single order for the
  lower level system.
\item[\tt AMQPDaemon] was a datatype explicitly constructed to serialize
  through \texttt{Aeson} into a JSON package that the lower level
  system could parse.
\end{description}

Here, the \texttt{SoundCommand} type encoded all orders that the game
masters and sound designers wanted to be able to give to the system, while
\texttt{DaemonSpec} abstractly encoded all command types the lower
level system accepted.

All datatypes derived \texttt{Read}, \texttt{Show} and \texttt{Generic},
which allowed us to automate parsing both from a Read-Evaluate-Print-Loop as well as
create automatic JSON parsers and encoders from \texttt{Aeson}. 

\subsection{SoundCommand}
\label{sec:soundspec}

Our separation of the sound scene description from the Haskell layer
control commands and the automated reactive triggers builded on their
separation into different datatypes. 
The family of datatypes we designed closely mirrored the task division
for the system. 
At the top-most abstraction
level, there was a data type \texttt{SoundCommand} enumerating the
various commands that can be given to the system. 

\begin{verbatim}
data SoundCommand = 
    Define Id SoundSpec |
    Commit | 
    Restore | 
    Diagnostic |
    ReadState |
    ReadSounds | 
    Compile SoundSpec |
    ReadOut SoundSpec | 
    Execute Id SoundSpec |
    Trigger Id FilterSpec SoundCommand |
    SoundCommand :++ SoundCommand |
    Declare Id SoundCommand |
    Call Id | 
    Delete Id | 
    Nop
    deriving (Eq, Show, Read, Generic)
\end{verbatim}

There were commands for interacting with the database state storage:
\texttt{Commit} and \texttt{Restore}; commands for debugging and
analyzing what a particular sound scene description was interpreted to:
\texttt{Diagnostic}, \texttt{ReadState}, \texttt{ReadOut}; commands
for naming and recalling both sound scenes and entire commands:
\texttt{Define}, \texttt{Declare}, \texttt{Execute}, \texttt{Call},
\texttt{Delete}; and commands for triggering sound scenes either
through events by \texttt{Trigger} or through direct command by
\texttt{Execute}. Finally, there was \texttt{ReadSounds} that reported
available sound scenes up to a top level UI layer, and
\texttt{:++} for chaining commands together as well as a \texttt{Nop}
that could finish an automatically generated chain of commands for
programmatic creation of triggers and action chains. 

The definition used the two types \texttt{SoundSpec} and
\texttt{FilterSpec} to parametrize its entries. These were given by

\begin{verbatim}
data SoundSpec = 
    Play File Segment Time Loudness |
    Loop File Segment Time Loudness |
    RadialDecay File Segment Time Loudness | 
    SoundSpec :+ SoundSpec | 
    Use Id |
    Stop Int Time |
    Fade Int Time Time Loudness |
    StopId Id Time |
    FadeId Id Time Time Loudness |
    NopS
    deriving (Eq, Show, Read, Generic)
\end{verbatim}
and
\begin{verbatim}
data FilterSpec = 
    FilterSpec :& FilterSpec |
    FilterSpec :| FilterSpec |
    MatchAll [FilterSpec] |
    MatchAny [FilterSpec] |
    MatchEvent String |
    MatchSender String |
    MatchKeyValue String String
    deriving (Eq, Show, Read, Generic, Ord)
\end{verbatim}

The sound scene specification allowed for playing a sound
by name or by index, either once or on an infinite loop, and for
stopping and changing volume. These were the operations understood by
the low level system as well.

In addition to these, the Haskell layer automatically generated
packages to smoothly fade between volume settings (for a spatially
distributed decaying sound scape) and for saving and recalling sound
scenes. After an initial attempt to design the fades, we ended up building
in support for remembering the last set volume for a sound, so that
fades could be given by a target loudness rather than by start and stop
loudness settings.

The inclusion of \texttt{Nop} and \texttt{NopS} helped us automate
generation of lists of actions; together with the concatenation
constructions given by \texttt{:++} and \texttt{:+}, there was a full
monoidal structure on both these datatypes, enabling easy generation
of composite commands from lists of command parameters. 

Since the entire system actively listened to the \texttt{AMQP} traffic of the
entire game system, it was easy to include a reactive component: using
a simple regular expressions-based recognition engine, we were able
to write simple rules that would when matched trigger arbitrary
pre-constructed \texttt{SoundCommand} actions. The rules were encoded
using the type \texttt{FilterSpec} and their corresponding actions were
encoded with the \texttt{Trigger} constructor of
\texttt{SoundCommand}. All \texttt{AMQP} messages in the game system contained a
sender, an event key and some collection of key-value pairs, all of
which could be regular expression matched with the rules specified as
\texttt{FilterSpec} entities.

\subsection{DaemonSpec}
\label{sec:daemonspec}

The \texttt{DaemonSpec} type encoded the abstract payload of a single
instruction to the low-level system. An element of type
\texttt{DaemonSpec} encoded all the descriptive information needed for
a low-level system command, without containing transient information
required for emitting any particular command package. In particular,
there was a serial ID number assigned to low-level command packages to
allow later commands to modify a running sound. These ID numbers were
not added in the \texttt{DaemonSpec} representation, but rather in the
next lower representation.

\begin{verbatim}
data DaemonSpec = 
    DaemonPlay Int Segment Time Loudness |
    DaemonLoop Int Segment Time Loudness |
    DaemonStop Int Time |
    DaemonSet Int Time Loudness 
              deriving (Eq, Show)
\end{verbatim}

\subsection{DaemonCommand}
\label{sec:daemoncommand}

The \texttt{DaemonCommand} encoded a message that could be sent to the
lower-level system. In particular, the command encoded a sequential
id-number used for later modifications of looping sounds and set up
a datatype for easy parsing for the receiving system.

\begin{verbatim}
data DaemonCommand = DaemonCommand {
      node :: Int,
      dcid :: Int,
      sound :: Int,
      time :: Int,
      volume_left :: Float,
      volume_right :: Float,
      command :: Int
    } deriving (Eq, Show, Generic)
\end{verbatim}

\subsection{AMQPDaemon}
\label{sec:amqpdaemon}

The type \texttt{AMQPDaemon} really only existed in order to wrap a
\texttt{DaemonCommand} item for serialization with \texttt{Aeson} and
transport in an \texttt{AMQP} package. The type is defined as:
\begin{verbatim}
data AMQPDaemon = AMQPDaemon {
      devent :: String,
      dsender :: String,
      dcmd :: DaemonCommand
    } deriving (Eq, Show, Generic)
\end{verbatim}
with custom JSON instances created by
\begin{verbatim}
instance FromJSON AMQPDaemon where
    parseJSON (Object v) = AMQPDaemon <$> 
                           v .: "event" <*>
                           v .: "sender" <*>
                           v .: "data"
    parseJSON _ = mzero

instance ToJSON AMQPDaemon where
    toJSON ad = object ["event" .= devent ad, 
                        "sender" .= dsender ad,
                        "data" .= dcmd ad]
\end{verbatim}

These were the only parser and encoder instances we wrote ourselves for
this project.


\subsection{Persistent state}
\label{sec:persistent-state}

There was a number of pieces of information the system needed access
to, with various levels of persistence. We designed a tiered state
type consisting of a serialisable section and a collection of
transient state properties, described by
\begin{verbatim}
data SST = SST {
      serst :: SerST,
      dbconn :: R.Connection,
      achan :: A.Channel,
      cmdid :: Int
    } 
\end{verbatim}
Here, we encoded instance-specific connection data for the \texttt{Redis} database in
\texttt{dbconn}, instance-specific connection data for the \texttt{AMQP}
communication channel in \texttt{achan}, and an instance counter for
sequential command ids in \texttt{cmdid}. The rest of the state was
stored in the \texttt{serst} (\emph{ser}ializiable \emph{st}ate) field,
which was saved
to the database in order to persist settings between runs.

The serializable state in turn was given by
\begin{verbatim}
data SerST = SerST {
      soundscapes :: M.HashMap String SoundSpec,
      commands :: M.HashMap String SoundCommand,
      triggers :: [(Id,(FilterSpec, SoundCommand))],
      loops :: [(Int, (Segment, Int, Loudness))],
      tags :: M.HashMap String [Int]
    } deriving (Show, Generic)
\end{verbatim}
where we make extensive use of the strict hashmap implementation from
the \texttt{unordered-containers} package.

Here, \texttt{soundscapes} saved all named sound scape descriptions;
\texttt{commands} saved all named sound system commands;
\texttt{triggers} saved trigger definitions and is iterated through
whenever a package showed up that the system might react to;
\texttt{loops} saved currently playing loops and their most recently
known loudness; and \texttt{tags} saved a lookup table from human
readable names to command ids.

In addition to these, there was a pair of hardcoded lists defined in
the source code itself: \texttt{playable} and \texttt{loopable}.
These two lists contained the names used throughout the sound system to
refer to all playable sounds, in an order kept synchronized with
playlists on both the lower level system daemon and on the MPD
instances. From these were also derived two hashmaps \texttt{playDict} and \texttt{loopDict} to enable faster index lookups given the names.

\subsection{Sound scape design daemon}
\label{sec:sound-scape-design}

The library described above was then used by a daemon which ran
throughout the game on one of the game control servers and reacted
dynamically to instructions arriving by \texttt{AMQP}. This daemon stored
the state in an
\texttt{IORef} and used the
callback structures in the \texttt{amqp} package to listen for and
react to \texttt{AMQP} messages. The entire logic of the server was encapsulated
in this callback and the functions it called.

The \texttt{callback} function parsed out the payload of the received \texttt{AMQP}
package and checked whether it matched a regular expression. If so, it parsed the contained command and acted on it; if not, it ran
the package through all defined patterns in \texttt{triggers} and ran
the associated action for each matching pattern. This linear lookup
may have been slower than more complex solutions, but it had the benefit
of being easy and reliable to design and was probably fast
enough for this application. In the end, as we will describe later,
there were some latency issues with the system as a whole. Our
diagnostics of the trigger list handling were inconclusive, but did
not seem to generate the observed latencies.

The function \texttt{action} executed a \texttt{SoundCommand} and
carried the entire logic of the system. This is where the data type was
translated into actual reactions. Most of the implementations were
straightforward: read the current state from the \texttt{IORef}
variable, extract relevant parts of the state, and then either assemble a package for
\texttt{AMQP} or for \texttt{Redis} and send it out, or modify the running state
according to the received instructions. By far the most involved of
these was the implementation of the \texttt{Execute} command,
responsible for sending out low-level instructions. This command constructed
\texttt{DaemonSpec} descriptions, assigned sequential command id
numbers, packed the result into \texttt{AMQP} packages, and -- depending on the
exact type of each command -- modified the state to remember the details
of the sent commands for later recall when constructing fades or
stops.

Large swathes of the daemon code were reused to construct a command
line interface that generated \texttt{AMQP} packages for controlling the
system, allowing for an accessible debugging and programming interface.


%%% Local Variables: 
%%% mode: latex
%%% TeX-engine: default
%%% TeX-master: "tmr"
%%% End: 
