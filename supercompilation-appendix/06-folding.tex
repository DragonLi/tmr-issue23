\section{\texttt{Folding.hs}}

The tree of configurations is folded into a graph using the 
well-known technique of ``tying the knot''
(\url{http://www.haskell.org/haskellwiki/Tying_the_Knot}).
We traverse the tree top-down, accumulating the encountered configurations.
If the current configuration coincides with a previous one modulo renaming,
then a loop is formed and we do not go inside the corresponding subtree.

\begin{lstlisting}[name=folding]
foldTree :: Tree Conf -> Graph Conf
foldTree t = fixTree (tieKnot []) t

tieKnot :: [Node Conf] -> Node Conf -> Tree Conf 
	-> Graph Conf
tieKnot ns n t@(Node e _) =
	case [(k, r) | k <- n:ns, isCall e, 
			Just r <- [renaming (nodeLabel k) e]] of
		[] -> fixTree (tieKnot (n:ns)) t
		(k, r):_ -> Node e (Fold k r)

fixTree :: (Node t -> Tree t -> Graph t) -> Tree t 
	-> Graph t
fixTree f (Node e (Transient c)) = t where
	t = Node e $ Transient $ f t c
fixTree f (Node e (Decompose cs)) = t where
	t = Node e $ Decompose [f t c | c <- cs]
fixTree f (Node e (Variants cs)) = t where
	t = Node e $ Variants [(p, f t c) | (p, c) <- cs]
fixTree f (Node e Stop) = (Node e Stop)
\end{lstlisting}

The following example is shown in the main article, subsection ``Folding''.
\begin{lstlisting}[style=demo]
-- demo15
ghci> foldTree $ buildTree (driveMachine prog1) 
	{{even(sqr(x))}}
...

\end{lstlisting}

We repeat the graph drawing here, marking coinciding (up to renaming) nodes
with the same color.

\begin{landscape}
\input{figures/folding}
\end{landscape}
